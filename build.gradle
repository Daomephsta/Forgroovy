buildscript {
    repositories {
        jcenter()
        maven { url = "http://files.minecraftforge.net/maven" }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT'
    }
}
apply plugin: 'groovy'
apply plugin: 'net.minecraftforge.gradle.forge'
//Only edit below this line, the above code adds and enables the necessary things for Forge to be setup.


version = project.mcVersion + "-" + project.modVersion
group = "daomephsta.forgroovy" // http://maven.apache.org/guides/mini/guide-naming-conventions.html
archivesBaseName = "Forgroovy"

sourceCompatibility = targetCompatibility = '1.8' // Need this here so eclipse task generates correctly.
compileJava {
    sourceCompatibility = targetCompatibility = '1.8'
}

minecraft 
{
    version = project.mcVersion + "-" + project.forgeVersion
    runDir = "run"
    mappings = project.mappingsVersion
    makeObfSourceJar = false
}

repositories
{
	jcenter()
}

dependencies
{
	compile 'org.codehaus.groovy:groovy:' + project.groovyVersion
	for (module in ['json', 'nio'])
	{
		compile "org.codehaus.groovy:groovy-${module}:" + project.groovyVersion
	}
}

processResources {
    // this will ensure that this task is redone when the versions change.
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    // replace stuff in mcmod.info, nothing else
    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
                
        // replace version and mcversion
        expand 'version':project.version, 'mcversion':project.minecraft.version
    }
        
    // copy everything else except the mcmod.info
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}

sourceSets {
	reobfuscator {
		java {
			srcDir 'src/reobfuscator/java'
			compileClasspath = sourceSets.main.compileClasspath
		}
		resources {
			srcDir 'src/reobfuscator/resources'
		}
	}
}

task compileTransformers(type: JavaCompile) {
	description 'Compiles the Forgroovy reobfuscating transformer'
	source(sourceSets.reobfuscator.java)
	classpath = sourceSets.reobfuscator.compileClasspath
	destinationDir = file("${buildDir}/compileTransformers")
}

task transformersJar(type: Jar, dependsOn: compileTransformers) {
	description 'Assembles the Forgroovy reobfuscating transformer into a jar'
	archiveName = 'ReobfuscatorTransformer.jar'
	from file("${buildDir}/compileTransformers")
	destinationDir = file("${buildDir}/libs")
	metaInf {
		from sourceSets.reobfuscator.resources
		include 'services/*'
		// Exclude duplicates
		duplicatesStrategy = 'exclude'
	}
}
